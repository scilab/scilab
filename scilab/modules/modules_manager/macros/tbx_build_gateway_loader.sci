// Scilab ( http://www.scilab.org/ ) - This file is part of Scilab
// Copyright (C) 2008 - INRIA - Simon LIPP <simon.lipp@scilab.org>
// Copyright (C) 2010 - DIGITEO - Pierre MARECHAL
//
// Copyright (C) 2012 - 2016 - Scilab Enterprises
//
// This file is hereby licensed under the terms of the GNU GPL v2.0,
// pursuant to article 5.3.4 of the CeCILL v.2.1.
// This file was originally licensed under the terms of the CeCILL v2.1,
// and continues to be available under such terms.
// For more information, see the COPYING file which you should have received
// along with this program.

// Generate a loader_gateway.sce script for the gateway
function tbx_build_gateway_loader(languages,path)

    rhs = argn(2);

    // Number of input arguments

    if and(rhs<> [1 2]) then
        error(msprintf(gettext("%s: Wrong number of input arguments: %d to %d expected.\n"),"tbx_build_gateway_loader",1,2));
    end

    // Input argument N°1
    // → languages

    if type(languages) <> 10 then
        error(msprintf(gettext("%s: Wrong type for input argument #%d: string expected.\n"),"tbx_build_gateway_loader",1));
    end

    // Input argument N°2
    // → path

    if rhs < 2 then
        path = pwd();

    else
        if type(path) <> 10 then
            error(msprintf(gettext("%s: Wrong type for input argument #%d: string expected.\n"),"tbx_build_gateway_loader",2));
        end

        if size(path,"*") <> 1 then
            error(msprintf(gettext("%s: Wrong size for input argument #%d: string expected.\n"),"tbx_build_gateway_loader",2));
        end

        if ~isdir(path) then
            error(msprintf(gettext("%s: The directory ''%s'' doesn''t exist or is not read accessible.\n"),"tbx_build_gateway_loader",path));
        end
    end

    // build the loader file

    mprintf(gettext("Generating loader_gateway.sce...\n"));

    loadertxt = [
    "// This file is released under the 3-clause BSD license. See COPYING-BSD.",
    "// Generated by builder_gateway.sce: Please, do not edit this file",
    "",
    "try"
    "    v = getversion(""scilab"");",
    "catch",
    "    v = [ 5 0 ]; // or older ",
    "end",
    "if (v(1) <= 5) & (v(2) < 3) then",
    "    // new API in scilab 5.3",
    "    error(gettext(""Scilab 5.3 or more is required.""));",
    "end",
    "",
    "sci_gateway_dir = get_absolute_file_path(""loader_gateway.sce"");",
    "current_dir     = pwd();",
    ""];

    for i = 1:size(languages,"*") do

        loadertxt = [loadertxt,
        "chdir(sci_gateway_dir);",
        "if ( isdir("""+languages(i)+""") ) then",
        "    chdir("""+languages(i)+""");",
        "    exec(""loader.sce"");",
        "end",
        ""];
    end

    loadertxt = [loadertxt,
    "chdir(current_dir);",
    "clear sci_gateway_dir current_dir v;"];

    loaderfile = pathconvert(path+"/loader_gateway.sce",%F);

    if ~mputl(loadertxt,loaderfile) then
        error(msprintf(gettext("%s: The file %s cannot been created, please check if you have write access on this file.\n"),"tbx_build_gateway_loader",loaderfile));
    end

endfunction
