// Scilab ( http://www.scilab.org/ ) - This file is part of Scilab
// Copyright (C) 2009-2011  - DIGITEO - Allan CORNET
//
// Copyright (C) 2012 - 2016 - Scilab Enterprises
//
// This file is hereby licensed under the terms of the GNU GPL v2.0,
// pursuant to article 5.3.4 of the CeCILL v.2.1.
// This file was originally licensed under the terms of the CeCILL v2.1,
// and continues to be available under such terms.
// For more information, see the COPYING file which you should have received
// along with this program.

function startModule()
  if %fftw then
    if getos() == "Windows" then
      librarynamefftw = SCI+"/bin/fftw/libfftw3-3"+getdynlibext();
      librarynamefftw3 = "";
    else
      librarynamefftw = "libfftw3"+getdynlibext();
      // Some distribs/os are not adding the .3 extension...
      librarynamefftw3 = librarynamefftw+".3";
    end

    if ( loadfftwlibrary(librarynamefftw3) | loadfftwlibrary(librarynamefftw)) then
      // replace scilab fft function by fftw
      clearfun('fft');
      newfun('fft', funptr('fftw'));

    else  // if ( loadfftwlibrary(librarynamefftw) )
      settings_file = pathconvert(SCIHOME+"/.scilab_settings", %f, %t)
      w = fileinfo(settings_file);
      show = %t;
      if w <> [] then
        show = grep(mgetl(SCIHOME+"/.scilab_settings"),"displayWarningFFTW=no")==[]
      end
      if show then
        MSG_FTTW = [_("You chose to install Scilab with the fftw module.");
                  _("This module requires: "+librarynamefftw+" (GPL licence).");
                  _("Scilab will use standard fft by default until you install this library.");
                  _("See help fftw for more information.")];

        if getscilabmode() == "STD" then
          messagebox(MSG_FTTW,"","info");
        else
          warning(MSG_FTTW);
        end
        mputl("displayWarningFFTW=no",SCIHOME+"/.scilab_settings")
      end
    end // if ( loadfftwlibrary(librarynamefftw) )
  end

endfunction

startModule();
clear startModule;
