// Scilab ( http://www.scilab.org/ ) - This file is part of Scilab
// Copyright (C) INRIA/ENPC
// 
// This file must be used under the terms of the CeCILL.
// This source file is licensed as described in the file COPYING, which
// you should have received as part of this distribution.  The terms
// are also available at    
// http://www.cecill.info/licences/Licence_CeCILL_V2-en.txt
//==========================================
// Generate a shared library which can be used by link command. 
function libn = ilib_for_link(names, ..
                              files, ..
                              libs, ..
                              flag, ..
                              makename, ..
                              loadername, ..
                              libname, ..
                              ldflags, ..
                              cflags, ..
                              fflags, ..
                              cc)

  [lhs,rhs] = argn(0);
  
  if rhs <= 4 then makename = "Makelib";end
  if rhs <= 5 then loadername = "loader.sce";end
  if rhs <= 6 then libname = ""; end
  if rhs <= 7 then ldflags = ""; end 
  if rhs <= 8 then cflags  = ""; end 
  if rhs <= 9 then fflags  = ""; end 
  if rhs <= 10 then cc  = ""; end 
  
  // generate a loader file
  if ( ilib_verbose() <> 0 ) then
    mprintf(gettext("   Generate a loader file\n"));
  end
  ilib_link_gen_loader(names,flag,loadername,libs,libname);
  
  // generate a Makefile
  if ( ilib_verbose() <> 0 ) then
    mprintf(gettext("   Generate a Makefile\n"));
  end

  ilib_link_gen_Make(names, ..
                     files, ..
                     libs, ..
                     makename, ..
                     libname, ..
		                 ldflags, ..
		                 cflags, ..
		                 fflags, ..
		                 cc, ..
		                 flag);
		                 
  // we call make
  if ( ilib_verbose() <> 0 ) then
    mprintf(gettext("   Running the Makefile\n"));
  end
  if (libname == "") then libname = names(1);end
  libn = ilib_compile('lib' + libname, makename, files);
  
endfunction
//==========================================
function ilib_link_gen_Make(names, ..
                            files, ..
                            libs, ..
                            makename, ..
                            libname, ..
                            ldflags, ..
                            cflags, ..
                            fflags, ..
                            cc, ..
                            flag)

  // generate a Makefile for gateway
  [lhs,rhs] = argn(0);
  if rhs <= 2 then libs = [];end
  if rhs <= 3 then makename = 'Makelib';end
  if rhs <= 4 then libname = "";end
  if rhs <= 5 then ldflags = ""; end 
  if rhs <= 6 then cflags  = ""; end 
  if rhs <= 7 then fflags  = ""; end 
  if rhs <= 8 then cc  = ""; end 
  if rhs <= 9 then flag  = "c"; end 
  
  if MSDOS then // Windows
    // Visual Studio C++ 
    if ( findmsvccompiler() <> 'unknown' ) then 
      Makename = makename+'.mak';
      ilib_link_gen_Make_msvc(names, ..
                              files, ..
                              libs, ..
                              Makename, ..
                              libname, ..
			                        ldflags, ..
			                        cflags, ..
			                        fflags, ..
			                        cc);
    else
      // LCC-WIN32
      if findlcccompiler() then
        Makename = makename+'.lcc';
  	    ilib_link_gen_Make_lcc(names, ..
  	                           files, ..
  	                           libs, ..
  	                           Makename, ..
  	                           libname, ..
  	                           ldflags, ..
  	                           cflags, ..
  	                           fflags, ..
  	                           cc, ..
  	                           flag);
      else
      // TO DO : Add another compiler here
      end
    end
  
  else // LINUX
   Makename = makename;

   ilib_gen_Make_unix(names, ..
                           files, ..
                           libs, ..
                           libname, ..
                           ldflags, ..
                           cflags, ..
                           fflags, ..
                           cc);
  end
  
endfunction
//==========================================

function ilib_link_gen_loader(names,flag,loadername,libs,libname)

  rhs = argn(2);
  if rhs <= 4 then libname = ""; end 
  if rhs <= 3 then libs=[]; end 
  if rhs <= 2 then loadername = 'loader.sce' ; end 
  
  lib_suf = getdynlibext();
  if libname=="" then libname = names(1);end 
  
  if ( length(libname) + length('_path') ) > 24 then
   shortlibname_path = part(libname,1:(24 - length('_path')));
  else
   shortlibname_path = libname;
  end
  
  fd=mopen(loadername,"wt");
  mfprintf(fd,"// ------------------------------------------------------\n");
  mfprintf(fd,"// generated by builder.sce: Please do not edit this file\n");
  mfprintf(fd,"// ------------------------------------------------------\n");
  mfprintf(fd,"\n");
  mfprintf(fd,"%s_path = get_absolute_file_path(''%s'');\n",shortlibname_path,basename(loadername+'.x'));
  mfprintf(fd,"\n");
  
  //** first "link" : external libraries 
  if libs=="" then 
    //** do nothing : you don't need to link any "external" lib(s)
  else
    //** add one "link" line for each library (with the appropriate extension)
      nl = size(libs,'*') ;
      for i=1:nl 
         if ( part(libs(i),1) == '/') then
           mfprintf(fd,"link(''%s%s'');\n",libs(i),lib_suf);
         else
           [diri, basenamei, exti] = fileparts(libs(i));
           if (diri == '') then
            mfprintf(fd,"link(%s_path+''%s%s'');\n",shortlibname_path,libs(i),lib_suf);
           else
            mfprintf(fd,"link(''%s%s'');\n",libs(i),lib_suf);
           end
         end
      end
  end  
  
  //** second "link" : user defined functions  
  mfprintf(fd,"link(%s_path+''lib%s%s'',[",shortlibname_path,libname,lib_suf);
  
  names = names(:)';
  n = size(names,'*');
  for i=1:n
    mfprintf(fd,"''%s''",names(i))
    if i <>n ; mfprintf(fd,","); else mfprintf(fd,"],");end
  end
  
  // we manage .f90 as .f on windows
  if MSDOS then 
   if findmsifortcompiler()<> 'unknown' then
     if flag == 'f90' then
      flag = 'f';
     end
   else
     if flag == 'f90' then
      error(gettext('F2C cannot build fortran 90'));
     end
   end
  end

  mfprintf(fd,"''%s'');\n",flag);
  mfprintf(fd,"// remove temp. variables on stack\n");
  mfprintf(fd,"clear %s_path;\n",shortlibname_path);
  mfprintf(fd,"clear get_file_path;\n");
  mfprintf(fd,"// ------------------------------------------------------\n");
  mclose(fd);
  
  if ilib_verbose() > 1 then
    disp(mgetl(loadername));
  end
  
endfunction
//==========================================    
function ilib_link_gen_Make_msvc(names, ..
                                  files, ..
                                  libs, ..
                                  Makename, ..
                                  libname, ..
                                  ldflags, ..
                                  cflags, ..
                                  fflags, ..
                                  cc)

  managed_ext = ['.cxx', '.cpp', '.c', '.f90', '.f'];
  obj_ext = ['.o', '.obj', ''];

  SCIDIR = SCI;
  SCIDIR1 = pathconvert(SCI,%f,%f,'w');
  LIBRARY = '';
  FILES_SRC = '';
  OBJS = '';
  OBJS_WITH_PATH = '';
  OTHERLIBS = '';
  CC = cc;
  CFLAGS = cflags;
  MEXCFLAGS = '';
  FFLAGS = fflags;
  MEXFFLAGS = '';
  LDFLAGS = ldflags;

  FILES_SRC_MATRIX = [];  
  
  [path_Make, file_Make, ext_Make] = fileparts(Makename);
  
  for i=1:size(files,'*') 
    [path_f, file_f, ext_f] = fileparts(files(i));

    if or(obj_ext == ext_f) then
      FILENAME = [];
      FILE_FOUNDED = %f;
      for y = managed_ext(:)'
        if (FILE_FOUNDED == %f) then
          if (fileinfo(path_f + file_f + y) <> []) | (fileinfo(path_Make + file_f + y) <> []) then
            FILENAME = path_f + file_f + y;
            FILE_FOUNDED = %t;
          end
        end
      end
    else
      FILENAME = files(i);
    end
    FILES_SRC_MATRIX = [FILES_SRC_MATRIX , FILENAME];
  end

  FILES_SRC = strcat(FILES_SRC_MATRIX,' ');
  
  OBJ_DEST_PATH = '';
  if (getenv("DEBUG_SCILAB_DYNAMIC_LINK","NO") == "NO") then
    OBJ_DEST_PATH = "Release/";
  else
    OBJ_DEST_PATH = "Debug/";
  end
  
  OBJS_MATRIX = [];
  OBJS_WITH_PATH_MATRIX = [];
  
  for y = 1:size(FILES_SRC_MATRIX,'*')
    [path_f, file_f, ext_f] = fileparts(FILES_SRC_MATRIX(y));
    OBJS_MATRIX = [OBJS_MATRIX, path_f + file_f + '.obj'];
    OBJS_WITH_PATH_MATRIX = [OBJS_WITH_PATH_MATRIX, OBJ_DEST_PATH + path_f + file_f + '.obj'];
  end
  
  OBJS = strcat(OBJS_MATRIX, ' ');
  OBJS_WITH_PATH =  strcat(OBJS_WITH_PATH_MATRIX, ' ');

  if (libname == '') then
    LIBRARY = 'lib' + names(1);
  else
    LIBRARY = 'lib' + libname;
  end

  if (libs <> []) then
    for x = libs(:)'
      if x <> '' then
        OTHERLIBS = OTHERLIBS + ' ' + x + '.lib';
      end
    end
  end

  try
    MAKEFILE_VC = mgetl(SCI+'/modules/dynamic_link/src/scripts/TEMPLATE_MAKEFILE.VC');
  catch
    MAKEFILE_VC = '';
  end
  
  MAKEFILE_VC = strsubst(MAKEFILE_VC, "__SCI__", SCIDIR);
  MAKEFILE_VC = strsubst(MAKEFILE_VC, "__SCIDIR1__", SCIDIR1);
  MAKEFILE_VC = strsubst(MAKEFILE_VC, "__LIBNAME__", LIBRARY);
  MAKEFILE_VC = strsubst(MAKEFILE_VC, "__FILES_SRC__", FILES_SRC);
  MAKEFILE_VC = strsubst(MAKEFILE_VC, "__OBJS__", OBJS);
  MAKEFILE_VC = strsubst(MAKEFILE_VC, "__OBJS_WITH_PATH__", OBJS_WITH_PATH);
  MAKEFILE_VC = strsubst(MAKEFILE_VC, "__OTHERSLIBS__", OTHERLIBS);
  
  if CC <> '' then
    MAKEFILE_VC = strsubst(MAKEFILE_VC, "__CC__",CC);
  else
    MAKEFILE_VC = strsubst(MAKEFILE_VC, "CC = __CC__","#CC = ");
  end
  
  MAKEFILE_VC = strsubst(MAKEFILE_VC, "__CFLAGS__", CFLAGS);
  MAKEFILE_VC = strsubst(MAKEFILE_VC, "__MEXCFLAGS__", MEXCFLAGS);
  MAKEFILE_VC = strsubst(MAKEFILE_VC, "__FFLAGS__", FFLAGS);
  MAKEFILE_VC = strsubst(MAKEFILE_VC, "__MEXFFLAGS__", MEXFFLAGS);
  MAKEFILE_VC = strsubst(MAKEFILE_VC, "__LDFLAGS__", LDFLAGS);

  if ( MAKEFILE_VC <> '') then
    fd = mopen(Makename, "wt");
    mputl(MAKEFILE_VC, fd);
    mclose(fd);
    if ilib_verbose() > 1 then
      disp(MAKEFILE_VC);
    end
  else
    // TEMPLATE_MAKEFILE.VC not found
    warning(SCI+'/modules/dynamic_link/src/scripts/TEMPLATE_MAKEFILE.VC'+ _('not found.') );
  end

endfunction
//==========================================    
function ilib_link_gen_Make_lcc(names, ..
                                files, ..
                                libs, ..
                                Makename, ..
                                libname, ..
                                ldflags, ..
                                cflags, ..
                                fflags, ..
                                cc, ..
                                flag)
                                
  // TO DO rewrite with a template 
  
  if libname == "" then libname = names(1);end 
  fd = mopen(Makename,"wt");
  mfprintf(fd,"# ------------------------------------------------------------\n");
  mfprintf(fd,"# generated by builder.sce (lcc): Please do not edit this file\n");
  mfprintf(fd,"# ------------------------------------------------------------\n\n");
  mfprintf(fd,"SCIDIR = %s\n",SCI);
  mfprintf(fd,"SCIDIR1 = %s\n",pathconvert(SCI,%f,%f,'w'));
  mfprintf(fd,"LCCLIBDIR =%s\n",getshortpathname(SCIHOME+filesep()+'lcclib'));
  mfprintf(fd,"DUMPEXTS = ""$(SCIDIR1)\\bin\\dumpexts""\n");
  mfprintf(fd,"SCIIMPLIB=$(LCCLIBDIR)\\LibScilab.lib $(LCCLIBDIR)\\blasplus.lib $(LCCLIBDIR)\\libf2c.lib $(LCCLIBDIR)\\intersci.lib $(LCCLIBDIR)\\lapack.lib $(LCCLIBDIR)\\scicos.lib\n\n");
  mfprintf(fd,"CC = lcc\n");
  mfprintf(fd,"LINKER = lcclnk\n");
  mfprintf(fd,"CFLAGS= -ansic msvcrt.lib -I""$(SCIDIR)/modules/core/includes"" -I""$(SCIDIR)/modules/output_stream/includes"" -I""$(SCIDIR)/libs/f2c"" -I""$(SCIDIR)/modules/mexlib/includes"" -Dmexfunction_=mex$*_ -DmexFunction=mex_$* -DWIN32 -DSTRICT -DFORDLL -D__STDC__  -DHAVE_EXP10"+ cflags +" \n"); 
  mfprintf(fd,"LINKER_FLAGS = -dll -nounderscores\n");
  mfprintf(fd,"EXTRA_LDFLAGS = "+ ldflags+"\n");
  mfprintf(fd,"O=.obj\n");
 
  mfprintf(fd,"# name of the dll to be built\n"); 
  mfprintf(fd,"LIBRARY = lib%s\n",libname);
  mfprintf(fd,"\n# list of objects file\n");
  
  if (flag =='c') then
  	mfprintf(fd,"OBJSC =");
  	for x = files(:)' ;
  	  [ptmp,ftmp,fext] = fileparts(x);
  		x = ptmp + ftmp;
   		mfprintf(fd," %s$(O)",x);
   	end
  
  	mfprintf(fd,"\nOBJSF=\n");
  else
  	mfprintf(fd,"OBJSC =\n");
  	mfprintf(fd,"\nOBJSF=");
  	for x = files(:)' ;
      [ptmp,ftmp,fext] = fileparts(x);
  		x = ptmp + ftmp;
   		mfprintf(fd," %s$(O)",x);
   	end
  end
  
  mfprintf(fd,"\nOBJS = $(OBJSC) $(OBJSF)\n");
  
  mfprintf(fd,"\n# added libraries \n");
  mfprintf(fd,"OTHERLIBS =");
  for x = libs(:)' ;
  	mfprintf(fd," ""%s.lib""",x);
  end
  mfprintf(fd,"\n");
  
  mfprintf(fd,"\nall :: $(LIBRARY).dll\n");
  mfprintf(fd,"\n$(LIBRARY).dll: $(OBJS)\n");
  mfprintf(fd,"\t"+"$(DUMPEXTS) -o ""$(LIBRARY).def"" ""$*"" $(OBJS)\n");
  mfprintf(fd,"\t"+"$(LINKER) $(LINKER_FLAGS) $(OBJS) $(OTHERLIBS) $(SCIIMPLIB) $(EXTRA_LDFLAGS) $*.def -o $(LIBRARY).dll\n\n");
 
  for x = files(:)' 
    [ptmp,ftmp,fext] = fileparts(x);
    x = ptmp + ftmp;
  	mfprintf(fd,"%s$(O):\n",x);
  	if (flag =='c') then
  		mfprintf(fd,"\t"+"$(CC) $(CFLAGS) $*.c\n\n");
  	else
  		mfprintf(fd,"\t"+"@$(SCIDIR1)\\bin\\f2c.exe $*.f \n");
		  mfprintf(fd,"\t"+"@$(CC) $(CFLAGS) $*.c \n");
		  mfprintf(fd,"\t"+"del $*.c \n");
	  end
  end
	
  mfprintf(fd,"clean:\n");
  mfprintf(fd,"\t"+"del *.obj\n");
  mfprintf(fd,"\t"+"del *.dll\n");
  mfprintf(fd,"\t"+"del *.lib\n");
  mfprintf(fd,"\t"+"del *.def\n");
 
  mclose(fd);
  
  if ilib_verbose() > 1 then
    disp(mgetl(Makename));
  end
endfunction
//==========================================    
