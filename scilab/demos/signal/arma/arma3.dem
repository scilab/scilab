mode(1)
// Spectral power estimation 
// ( form Sawaragi et all) 
// Copyright Enpc
m=18
a=[1,-1.3136,1.4401,-1.0919,+0.83527]
b=[0.0,0.13137,0.023543,0.10775,0.03516]
u=rand(1,1000,'n');
z=arsimul(a,b,[0],0,u);
//----Using macro mese 
[sm,fr]=mese(z,m);
//----The theorical result 
deff('[gx]=gxx(z)',['gx=(abs( a* exp(-%i*2*%pi*z*(0:4))'')**2)';...
      'gx=abs( b* exp(-%i*2*%pi*z*(0:4))'')**2/gx']);
res=[];
for x=fr,res=[ res, gxx(x)];end;
//----using armax estimation of order  (4,4)
// it's a bit tricky because we are not supposed to know the order
//
[arc,la,lb,sig,resid]=armax(4,4,z,u);
a=la(1);
b=lb(1);
res1=[];
for x=fr,res1=[ res1, gxx(x)];end;
//
leg="log(p) :using macro mese @ theoriqal value@log(p) : arma identifcation"
halt();
xbasc();
xtitle('Spectral power','frequency','spectral estimate')
plot2d([fr;fr;fr]',[20*log(sm/sm(1))/log(10);...
  20*log(res/res(1))/log(10);...
  20*log(res1/res1(1))/log(10)]',...
 [2,1,-1],"111",leg, [0,-70,0.5,60]);




